;
; ======== Hwi_asm.s62 ========
;
;! Revision History
;! ================
;! 14-Dec-2009 agd	tag Module state struct
;! 19-Mar-2007 cmcc     Changed switchToIsrStack for ROM
;! 12-Sep-2006 cmcc     Added .sect and .clink to all functions
;! 24-Mar-2006 agd      Added switchToIsrStack and switchToTaskStack
;! 16-Aug-2005 agd      created
;

        .cdecls C,NOLIST,"package/internal/Hwi.xdc.h" 

_ti_sysbios_family_c64_Hwi_Module__state__V .tag ti_sysbios_family_c64_Hwi_Module_State

        .text

SP              .set    b15


;
;  ======== ti_sysbios_family_c64_Hwi_switchToIsrStack ========
;  Char *ti_sysbios_family_c64_Hwi_switchToIsrStack();
;
;  If not already on the isr stack, switch to it.
;
;  Checks the Hwi module object's taskSP. 
;  If 0, then set taskSP to current SP then switch to ISR stack.
;
;  Returns:
;        old taskSP value for use with switchToTaskStack()
;

        .global _ti_sysbios_family_xxx_Hwi_switchToIsrStack

    .sect ".text:_ti_sysbios_family_xxx_Hwi_switchToIsrStack"
	.clink
_ti_sysbios_family_xxx_Hwi_switchToIsrStack:
        .asmfunc
        mvkl    _ti_sysbios_family_c64_Hwi_Module__state__V, a0
        mvkh    _ti_sysbios_family_c64_Hwi_Module__state__V, a0
        ldw     *a0(ti_sysbios_family_c64_Hwi_Module_State.taskSP), b0   ; get current taskSP value 
        ldw     *a0(ti_sysbios_family_c64_Hwi_Module_State.isrStack), b1
||      b       b3
        nop     3                       ; wait total of 5 cycles for branch
        mv      b0, a4                  ; 1st ldw to b0 complete
  [!b0] stw     SP, *a0(ti_sysbios_family_c64_Hwi_Module_State.taskSP)   ; save old SP
||[!b0] mv      b1, SP                  ; return SP on isrStack
        .endasmfunc

;
;  ======== ti_sysbios_family_c64_Hwi_switchToTaskStack ========
;  ti_sysbios_family_c64_Hwi_switchToTaskStack(Char *oldTaskSP);
;
;  If at bottom of ISR stack, switch to Task stack.
;
;  If oldTaskSP is zero, then this is the thread that switched
;  to the ISR stack first and therefore it is safe to switch
;  back to the task stack address saved in the Hwi module 
;  object's taskSP variable.
;
;

        .global _ti_sysbios_family_xxx_Hwi_switchToTaskStack

    .sect ".text:_ti_sysbios_family_xxx_Hwi_switchToTaskStack"
	.clink
_ti_sysbios_family_xxx_Hwi_switchToTaskStack:
        .asmfunc
        mv      a4, b0
||      mvkl    _ti_sysbios_family_c64_Hwi_Module__state__V.taskSP, a0
        mvkh    _ti_sysbios_family_c64_Hwi_Module__state__V.taskSP, a0
||      b       b3
  [!b0] ldw     *a0, SP
  [!b0] stw     b0, *a0
        nop     3
        .endasmfunc

